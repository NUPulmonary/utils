import pandas as pd
import os

WRAPPER_DIR = "/projects/b1038/Pulmonary/nmarkov/utils/snakemake/wrappers"
DATA_DIR = config['data_dir']
SAMPLE_CSV_PATH = config['sample_csv_path'] 
TRANSCRIPTOME = config['transcriptome']
CELLRANGER_DIR = config['CELLRANGER_DIR']
SAMPLES = pd.read_csv(SAMPLE_CSV_PATH)
os.chdir(DATA_DIR)

rule all:
    input:
        f"{DATA_DIR}/cellranger/metrics_summary.csv",

rule sample_sheet:
    input: SAMPLE_CSV_PATH
    output: f"{DATA_DIR}/{{run_id,\w+}}.csv"
    params:
        slurm__skip=True
    wrapper:
        f"file://{WRAPPER_DIR}/sample_sheet"

rule demultiplex:
    input:
        "../../raw/{run_id}",
        "{dir}/{run_id,\w+}.csv"
    output: directory("{dir}/fastq-run/{run_id}")
    params:
        slurm__account="b1042",
        slurm__partition="genomics",
        slurm__cores=4,
        slurm__hours=2,
        slurm__mem=40,
        cellranger_dir=CELLRANGER_DIR,
        keep_cellranger_output=True,
    wrapper:
        f"file://{WRAPPER_DIR}/mkfastq"

rule cellranger:
    input: 
        lambda x: [f"{DATA_DIR}/fastq-run/{r}" for r in SAMPLES.RunID[SAMPLES.Sample == x.sample].values]
    output: directory(f"{DATA_DIR}/cellranger/{{sample,\w+}}")
    params:
        slurm__account="b1042",
        slurm__partition="genomics",
        slurm__cores=8,
        slurm__hours=16,
        slurm__mem=60,
        chemistry="auto",
        transcriptome=TRANSCRIPTOME,
        cellranger_dir=CELLRANGER_DIR,
        sample_csv_path = SAMPLE_CSV_PATH
    wrapper:
        f"file://{WRAPPER_DIR}/cellranger"

rule metrics_summary:
    input: expand("{{dir}}/cellranger/{sample}", sample=SAMPLES.Sample.unique())
    output: "{dir}/cellranger/metrics_summary.csv"
    params:
        slurm__skip=True
    wrapper:
        f"file://{WRAPPER_DIR}/metrics_summary"